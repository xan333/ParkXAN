#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс
	
	Процедура ПеренестиФотоВПрисоединенныеФайлы(ПараметрыОбработки) Экспорт
		
		ПараметрыОбработки.ОбработкаЗавершена=ложь;	
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ Первые 50
			|	Аттракционы.Ссылка
			|ИЗ
			|	Справочник.Аттракционы КАК Аттракционы
			|ГДЕ
			|	НЕ Аттракционы.ФотоПеренесено";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ПараметрыОбработки.ОбработкаЗавершена=истина;
			Возврат;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НачатьТранзакцию();
			Попытка
				СпрОбъект=ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
				ДанныеФото=СпрОбъект.УдалитьФото.Получить();
				Если ДанныеФото<>Неопределено Тогда
					ПеренестиФотоВПрисоединенныйФайл(СпрОбъект,ДанныеФото);					
				КонецЕсли;
				СпрОбъект.ФотоПеренесено=истина;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СпрОбъект);
				ЗафиксироватьТранзакцию();	
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
		КонецЦикла;
				
	КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПеренестиФотоВПрисоединенныйФайл(СпрОбъект,ДанныеФото)
	АдресДанныхФайла=ПоместитьВоВременноеХранилище(ДанныеФото);
	ПараметрыДобавления=РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыДобавления.ВладелецФайлов=СпрОбъект.Ссылка;
	ПараметрыДобавления.ИмяБезРасширения="image";
	ПараметрыДобавления.РасширениеБезТочки="jpg";
	ПрисоединенныйФайл=РаботаСФайлами.ДобавитьФайл(ПараметрыДобавления, АдресДанныхФайла);	
	СпрОбъект.Фото=ПрисоединенныйФайл;
	СпрОбъект.УдалитьФото=Неопределено;
	//Тест изменений
КонецПроцедуры

#КонецОбласти

#КонецЕсли